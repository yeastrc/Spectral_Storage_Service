/*
 *  Application 'Scan_File_Processor_Importer_GRADLE' folder
 
 * 	Build Runnable Jar for Processing Scan File
 
 		defaultTasks 'shadowJar'
 */

/*
    Build executable jar file with gradle shadowJar
    More info at: https://imperceptiblethoughts.com/shadow/
    
    Requires Gradle 5+
 */

	//  http://ajoberstar.org/grgit/grgit-gradle.html
	// Grgit: Generally, you should only apply the plugin to the root project of your build.
	//  Plugin adds grgit property to your build, which is an instance of Grgit.
	//     Plugin looks for .git directory in build directory and it's parents
 
buildscript {
  repositories {
    maven {
      url "https://plugins.gradle.org/m2/"
    }
  }
  dependencies {
    classpath "org.ajoberstar.grgit:grgit-gradle:3.1.1"
  }
}

plugins {
	id 'com.github.johnrengelman.shadow' version '5.0.0'
	//  moved to 'buildscript'. See Notes above	id 'org.ajoberstar.grgit' version '3.1.1'
}

apply plugin: 'java'

apply plugin: "org.ajoberstar.grgit"

sourceCompatibility = 1.8      //    defaults to currently installed JVM
// targetCompatibility  = 1.8  //    defaults to sourceCompatibility

repositories {
    mavenCentral()
    jcenter()
}

//  Set default task for this build.gradle file
defaultTasks 'shadowJar'



dependencies {

//  Specify dependency on Common_Accept_Import_Webapp_Client_Connector_Library_GRADLE
	
	compile project(":Common_Importer_Get_Data_Client_Connector_GRADLE")

	compile project(":Common_Spectral_File_Core_GRADLE")
	
	compile project(":Common_Webapps_Importer_GRADLE")
	
	compile project(":Input_ScanFile_mzML_mzXML_FileParse_Code_GRADLE")
	
	//  Jars not found in any repository
	
	compile fileTree(dir: './libs_not_in_any_repository', include: ['*.jar'])
	
	
}

//  Assign to grgit, created by plugin
def gitRepo = grgit


// def gitRepo = Grgit.open({
//  dir = ".."  //  Use parent directory since that is root of GIT repo
//  currentDir = <path>
//  credentials = <credentals>
// })

def gitHeadId = gitRepo.head().id
def gitCommit = gitRepo.head().getAbbreviatedId(8)
def gitBranch = gitRepo.branch.getCurrent().getName()

//  Get tag at GIT HEAD: like git tag --points-at HEAD
def repoHead = gitRepo.head()
def repoHeadTag = gitRepo.tag.list().find {
	it.commit == repoHead
}
def repoTagName = "NO GIT Tag at HEAD"

if ( repoHeadTag ) {
	println("GIT Tag at HEAD found")
	repoTagName = repoHeadTag.getName()
}


println("gitHeadId: " + gitHeadId )
println("gitCommit: " + gitCommit )
println("gitBranch: " + gitBranch )
println("GIT Tag at HEAD: " + repoTagName )

jar {
    manifest {
        attributes 'Main-Class': 'org.yeastrc.spectral_storage.scan_file_processor.program.Scan_File_Processor_MainProgram'
        attributes([ 'Project-Title': project.name,
        			'Project-URL' : 'https://github.com/yeastrc/Spectral_Storage_Service',
					'Build-Timestamp': new Date().format("yyyy-MM-dd HH:mm:ssZ"),
					'Git-Commit': gitCommit,
					'GIT-Full-HASH': gitHeadId,
					'GIT-Tag-at-HEAD': repoTagName,
					'Git-Branch': gitBranch ])
    }
}

//  For creating runnable jar
processResources {
    from ('src/main/resources') {
        include '**/*'
    }
}

shadowJar {
    baseName = 'spectralStorage_ProcessScanFile'
    classifier = null
    version = null
}

